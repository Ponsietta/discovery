PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX nmo: <http://nomisma.org/ontology#>

PREFIX lpviz: <http://visualization.linkedpipes.com/ontology/>

DELETE {
  ?s nmo:hasStartDate ?sd ;
	nmo:hasEndDate ?ed .
} INSERT {
  ?s lpviz:hasAbstraction ?abstraction .

  ?abstraction a time:Interval ;
    time:hasBeginning ?abstractionBeginning ;
	time:hasEnd ?abstractionEnd ;
	rdfs:label ?abstractionLabel .
  
  ?abstractionBeginning a time:Instant ;
	time:inXSDDateTime ?start .
	
  ?abstractionEnd a time:Instant ;
	time:inXSDDateTime ?end .
} WHERE {
  ?s nmo:hasStartDate ?start ;
	nmo:hasEndDate ?end ;
	skos:prefLabel ?label .
  
  BIND(CONCAT("Existence of ", STR(?label)) AS ?abstractionLabel)
  
  BIND(IRI(CONCAT(STR(?s), "/abstraction/nomisma-start-date-end-date2time-Interval")) AS ?abstraction)
  BIND(IRI(CONCAT(STR(?s), "/abstraction/nomisma-start-date-end-date2time-Interval/beginning")) AS ?abstractionBeginning)
  BIND(IRI(CONCAT(STR(?s), "/abstraction/nomisma-start-date-end-date2time-Interval/end")) AS ?abstractionEnd)
  
}